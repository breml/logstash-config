// Parsing rules based on https://github.com/elastic/logstash/blob/master/logstash-core/lib/logstash/config/grammar.treetop
{
    package config
}

// Parsing entry point
init =
    #{
        return initState(c)
    }
    &{
        return initParser()
    } conf:(
        conf:config EOF {
            return ret(conf)
        } / _ EOF {
            return ast.NewConfig(nil, nil, nil), nil
        }
    ) {
        return retConfig(c, conf)
    }

//  rule config
//    _ plugin_section _ (_ plugin_section)* _ <LogStash::Config::AST::Config>
//  end

// Removed _ after initial pluginSection (ps), because it is not needed.

config =
    psComment:_ ps:pluginSection pss:(
        psComment:_ ps:pluginSection {
            return configSection(ps, psComment)
        }
    )* footerComment:_ EOF {
        return config(ps, pss, psComment, footerComment)
    }

//  rule comment
//    (whitespace? "#" [^\r\n]* "\r"? "\n")+ <LogStash::Config::AST::Comment>
//  end

comment =
    (whitespace? "#" [^\r\n]* "\r"? ("\n" / EOF))+ {
      return comment(c)
    }

//  rule _
//    (comment / whitespace)* <LogStash::Config::AST::Whitespace>
//  end

_ =
    (comment / whitespace)*

// Add __ rule, which keeps compatibility, with Logstash, but allows to warn
// about comments in exceptional locations instead of just silently ignore them.

__ =
    ( (comment / whitespace)* {
        return c.text, nil
    } ) #{
        return warnComment(c)
    }

//  rule whitespace
//    [ \t\r\n]+ <LogStash::Config::AST::Whitespace>
//  end

whitespace =
    [ \t\r\n]+ {
      return whitespace()
    }

//  rule plugin_section
//    plugin_type _ "{"
//       _ (branch_or_plugin _)*
//    "}"
//    <LogStash::Config::AST::PluginSection>
//  end

// Moved _ after "{" to plugin / branch section to simplify comment handling
// Moved _ footerComment outside of ( ... )* seciont to simplify comment handling

pluginSection =
    pt:pluginType __ "{" bops:(
        bop:branchOrPlugin {
            return ret(bop)
        }
    )* footerComment:_ (
        "}" / &{
            return pushError("expect closing curly bracket", c)
        }
    ) {
        return pluginSection(pt, bops, footerComment)
    }

//   rule branch_or_plugin
//     branch / plugin
//   end

branchOrPlugin =
    branch / plugin

//   rule plugin_type
//     ("input" / "filter" / "output")
//   end

pluginType =
    "input" {
        return ast.Input, nil
    } / "filter" {
        return ast.Filter, nil
    } / "output" {
        return ast.Output, nil
    } / &{
        return pushError("expect plugin type (input, filter, output)", c)
    }

//   rule plugins
//     (plugin (_ plugin)*)?
//     <LogStash::Config::AST::Plugins>
//   end

//   rule plugin
//     name _ "{"
//       _
//       attributes:( attribute (whitespace _ attribute)*)?
//       _
//     "}"
//     <LogStash::Config::AST::Plugin>
//   end

// Moved _ after "{" inside block attributes before attribute to simplify comment handling

plugin =
    comment:_ name:name __ "{" attributes:(
        comment:_ attribute:attribute attrs:(
            whitespace comment:_ attribute:attribute {
                return attributeComment(attribute, comment, true)
            }
        )* {
            return attributes(attribute, attrs, comment)
        }
    )? footerComment:_ (
        "}" / &{
            return fatalError("expect closing curly bracket", c)
        }
    ) {
        return plugin(name, attributes, comment, footerComment)
    }

//   rule name
//     (
//       ([A-Za-z0-9_-]+ <LogStash::Config::AST::Name>)
//       / string
//     )
//   end

name =
    ( ([A-Za-z0-9_-]+) {
        return string(c.text), nil
    } / value:stringValue {
        return ret(value)
    } )

//   rule attribute
//     name _ "=>" _ value
//     <LogStash::Config::AST::Attribute>
//   end

attribute =
    name:name __ "=>" __ value:value {
        return attribute(name, value)
    }

//   rule value
//     plugin / bareword / string / number / array / hash
//   end

value =
    plugin / bareword / stringValue / number / array / hash / &{
        return pushError("invalid value", c)
    }

//   rule array_value
//     bareword / string / number / array / hash
//   end

arrayValue =
    bareword / stringValue / number / array / hash / &{
        return fatalError("invalid array value", c)
    }

//   rule bareword
//     [A-Za-z_] [A-Za-z0-9_]+
//     <LogStash::Config::AST::Bareword>
//   end

bareword =
    [A-Za-z_] [A-Za-z0-9_]+ {
        return ast.NewStringAttribute("", string(c.text), ast.Bareword), nil
    }

//   rule double_quoted_string
//     ( '"' ( '\"' / !'"' . )* '"' <LogStash::Config::AST::String>)
//   end

doubleQuotedString =
    ( `"` ( `\"` / !`"` . )* (
        `"` / &{
            return fatalError("expect closing double quotes (\")", c)
        }
    ) ) {
        return enclosedValue(c)
    }

//   rule single_quoted_string
//     ( "'" ( "\\'" / !"'" . )* "'" <LogStash::Config::AST::String>)
//   end

singleQuotedString =
    ( `'` ( `\'` / !`'` . )* (
        `'` / &{
            return fatalError("expect closing single quote (')", c)
        }
    ) ) {
        return enclosedValue(c)
    }

//   rule string
//     double_quoted_string / single_quoted_string
//   end

stringValue =
    str:( str:doubleQuotedString {
        return ast.NewStringAttribute("", str.(string), ast.DoubleQuoted), nil
    } / str:singleQuotedString {
        return ast.NewStringAttribute("", str.(string), ast.SingleQuoted), nil
    } ) {
        return ret(str)
    }

//   rule regexp
//     ( '/' ( '\/' / !'/' . )* '/'  <LogStash::Config::AST::RegExp>)
//   end

regexp =
    ( `/` ( `\/` / !`/` . )* (
        `/` / &{
            return fatalError("expect closing slash (/) for regexp", c)
        }
    ) ) {
        return regexp(c)
    }

//   rule number
//     "-"? [0-9]+ ("." [0-9]*)?
//     <LogStash::Config::AST::Number>
//   end

number =
    "-"? [0-9]+ ("." [0-9]*)? {
        return number(string(c.text))
    }

//   rule array
//     "["
//     _
//     (
//       value (_ "," _ value)*
//     )?
//     _
//     "]"
//     <LogStash::Config::AST::Array>
//   end

// TODO: value should be changed to to array_value due to https://github.com/elastic/logstash/issues/6580

// Moved _ after "[" inside values infront of first value:value to simplify comment handling

array =
    "[" values:(
        comment:_ value:value values:(
            __ "," comment:_ value:value {
                return attributeComment(value, comment, true)
            }
        )* {
            return attributes(value, values, comment)
        }
    )? footerComment:_ (
        "]" / &{
            return fatalError("expect closing square bracket", c)
        }
    ) {
        return array(values, footerComment)
    }

//   rule hash
//     "{"
//       _
//       hashentries?
//       _
//     "}"
//     <LogStash::Config::AST::Hash>
//   end

// Moved _ after "{" to hashentry section to simplify comment handling

hash =
    "{" entries:hashentries? footerComment:_ (
        "}" / &{
            return fatalError("expect closing curly bracket", c)
        }
    ) {
        return hash(entries, footerComment)
    }

//   rule hashentries
//     hashentry (whitespace hashentry)*
//     <LogStash::Config::AST::HashEntries>
//   end

hashentries =
    hashentry:hashentry hashentries1:(
        whitespace hashentry:hashentry {
            return ret(hashentry)
        }
    )* {
        return hashentries(hashentry, hashentries1)
    }

//   rule hashentry
//     name:(number / bareword / string) _ "=>" _ value
//     <LogStash::Config::AST::HashEntry>
//   end

hashentry =
    comment:_ name:(number / bareword / stringValue) __ "=>" __ value:value {
        return hashentry(name, value, comment)
    }

// Conditions

//   rule branch
//     if (_ else_if)* (_ else)?
//     <LogStash::Config::AST::Branch>
//   end

// Added _ before ifBlock to simplify comment handling

branch =
    ifComment:_ ifBlock:ifCond elseIfBlocks:(
        eibComment:_ eib:elseIf {
                return elseIfComment(eib, eibComment)
            }
        )* elseBlock:(
            ebComment:_ eb:elseCond {
                return elseComment(eb, ebComment)
            }
        )? {
        return branch(ifBlock, elseIfBlocks, elseBlock, ifComment)
    }

//   rule if
//     "if" _ condition _ "{" _ (branch_or_plugin _)* "}"
//     <LogStash::Config::AST::If>
//   end

// Moved _ after "{" into bops to simplify comment handling
// Moved _ after bop outside of bops to simplify comment handling

ifCond =
    "if" __ cond:condition __ "{" bops:(
            comment:_ bop:branchOrPlugin {
                return branchOrPluginComment(bop, comment)
            }
        )* footerComment:_ (
            "}" / &{
                return fatalError("expect closing curly bracket", c)
            }
        ) {
        return ifBlock(cond, bops, footerComment)
    }

//   rule else_if
//     "else" _ "if" _ condition _ "{" _ ( branch_or_plugin _)* "}"
//     <LogStash::Config::AST::Elsif>
//   end

// Moved _ after "{" into bops to simplify comment handling
// Moved _ after bop outside of bops to simplify comment handling

elseIf =
    "else" __ "if" __ cond:condition __ "{" bops:(
        comment:_ bop:branchOrPlugin {
            return branchOrPluginComment(bop, comment)
        }
    )* footerComment:_ (
        "}" / &{
            return fatalError("expect closing curly bracket", c)
        }
    ) {
        return elseIfBlock(cond, bops, footerComment)
    }

//   rule else
//     "else" _ "{" _ (branch_or_plugin _)* "}"
//     <LogStash::Config::AST::Else>
//   end

// Moved _ after "{" into bops to simplify comment handling
// Moved _ after bop outside of bops to simplify comment handling

elseCond =
    "else" __ "{" bops:(
        comment:_ bop:branchOrPlugin {
            return branchOrPluginComment(bop, comment)
        }
    )* footerComment:_ (
        "}" / &{
            return fatalError("expect closing curly bracket", c)
        }
    ) {
        return elseBlock(bops, footerComment)
    }

//   rule condition
//     expression (_ boolean_operator _ expression)*
//     <LogStash::Config::AST::Condition>
//   end

condition =
    cond:expression conds:(
        __ bo:booleanOperator __ cond:expression {
            return expression(bo, cond)
        }
    )* {
        return condition(cond, conds)
    }

//   rule expression
//     (
//         ("(" _ condition _ ")")
//       / negative_expression
//       / in_expression
//       / not_in_expression
//       / compare_expression
//       / regexp_expression
//       / rvalue
//     ) <LogStash::Config::AST::Expression>
//   end

expression =
    (
        (
            "(" __ cond:condition __ ")" {
                return conditionExpression(cond)
            }
        )
      / negativeExpression
      / inExpression
      / notInExpression
      / compareExpression
      / regexpExpression
      / rv:rvalue {
          return rvalue(rv)
      }
      // &{ return pushError("expect valid expression", c) }
    )

//   rule negative_expression
//     (
//         ("!" _ "(" _ condition _ ")")
//       / ("!" _ selector)
//     ) <LogStash::Config::AST::NegativeExpression>
//   end

negativeExpression =
    (
        (
            "!" __ "(" __ cond:condition __ (
                ")" / &{
                    return fatalError("expect closing parenthesis", c)
                }
            ) {
                return negativeExpression(cond)
            }
        )
      / (
          "!" __ sel:selector {
              return negativeSelector(sel)
          }
        )
    )

//   rule in_expression
//     rvalue _ in_operator _ rvalue
//     <LogStash::Config::AST::InExpression>
//   end

inExpression =
    lv:rvalue __ inOperator __ rv:rvalue {
        return inExpression(lv, rv)
    }

//   rule not_in_expression
//     rvalue _ not_in_operator _ rvalue
//     <LogStash::Config::AST::NotInExpression>
//   end

notInExpression =
    lv:rvalue __ notInOperator __ rv:rvalue {
        return notInExpression(lv, rv)
    }

//   rule in_operator
//     "in"
//   end

inOperator =
    "in" / &{
        return pushError("expect in operator (in)", c)
    }

//   rule not_in_operator
//     "not " _ "in"
//   end

notInOperator =
    "not " __ "in" / &{
        return pushError("expect not in operator (not in)", c)
    }

//   rule rvalue
//     string / number / selector / array / method_call / regexp
//   end

// Removed method_call due to https://github.com/elastic/logstash/issues/6698
// rvalue =
//     string_value / number / selector / array / method_call / regexp
rvalue =
    stringValue / number / selector / array / regexp / &{
        return pushError("invalid value for expression", c)
    }

//   rule method_call
//       method _ "(" _
//         (
//           rvalue ( _ "," _ rvalue )*
//         )?
//       _ ")"
//     <LogStash::Config::AST::MethodCall>
//   end

// Removed method_call due to https://github.com/elastic/logstash/issues/6698
// method_call  =
//       method _ "(" _
//         (
//           rvalue ( _ "," _ rvalue )*
//         )?
//       _ ")"

//   rule method
//     bareword
//   end

// Removed method_call due to https://github.com/elastic/logstash/issues/6698
// method =
//     bareword

//   rule compare_expression
//     rvalue _ compare_operator _ rvalue
//     <LogStash::Config::AST::ComparisonExpression>
//   end

compareExpression =
    lv:rvalue __ co:compareOperator __ rv:rvalue {
        return compareExpression(lv, co, rv)
    }

//   rule compare_operator
//     ("==" / "!=" / "<=" / ">=" / "<" / ">")
//     <LogStash::Config::AST::ComparisonOperator>
//   end

compareOperator =
    ("==" / "!=" / "<=" / ">=" / "<" / ">" / &{
        return pushError("expect compare operator (==, !=, <=, >=, <, >)", c)
    }) {
        return compareOperator(string(c.text))
    }

//   rule regexp_expression
//     rvalue _  regexp_operator _ (string / regexp)
//     <LogStash::Config::AST::RegexpExpression>
//   end

regexpExpression =
    lv:rvalue __  ro:regexpOperator __ rv:(stringValue / regexp) {
        return regexpExpression(lv, ro, rv)
    }

//   rule regexp_operator
//     ("=~" / "!~") <LogStash::Config::AST::RegExpOperator>
//   end

regexpOperator =
    ("=~" / "!~" / &{
        return pushError("expect regexp comparison operator (=~, !~)", c)
    }) {
        return regexpOperator(string(c.text))
    }

//   rule boolean_operator
//     ("and" / "or" / "xor" / "nand")
//     <LogStash::Config::AST::BooleanOperator>
//   end

booleanOperator =
    ("and" / "or" / "xor" / "nand" / &{
        return pushError("expect boolean operator (and, or, xor, nand)", c)
    }) {
        return booleanOperator(string(c.text))
    }

//   rule selector
//     selector_element+
//     <LogStash::Config::AST::Selector>
//   end

selector =
    ses:selectorElement+ {
        return selector(ses)
    }

//   rule selector_element
//     "[" [^\]\[,]+ "]"
//     <LogStash::Config::AST::SelectorElement>
//   end

selectorElement =
    "[" [^\][,]+ (
        "]" / &{
            return pushError("expect closing square bracket", c)
        }
    ) {
        return selectorElement(string(c.text))
    }

EOF = !.